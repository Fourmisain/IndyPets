import java.nio.file.*
import java.nio.file.attribute.BasicFileAttributes

plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '2.+'
	id 'minepkg-publish' version 'latest.release'
}

version = project.mod_version + '-' + project.minecraft_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	accessWidenerPath = file("src/main/resources/indypets.accesswidener")

	runs {
		client {
			vmArgs "-Dmixin.debug.export=true"
			programArgs "--username", "Test"
		}
	}
}

repositories {
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/" }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	maven { url = "https://maven.bawnorton.com/releases" }
	maven { url = "https://maven.enjarai.dev/mirrors" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixin_squared_version}")))

	modImplementation "maven.modrinth:followers-teleport-too:1.21.9-2.7-fabric+forge+neo"
	modRuntimeOnly "maven.modrinth:collective:1.21.9-8.9-fabric+forge+neo"
//	modRuntimeOnly "maven.modrinth:friends-and-foes:fabric-mc1.21.1-3.0.3"
}

processResources {
	def stringProperties = providers.gradlePropertiesPrefixedBy('').get()
	inputs.properties stringProperties

	filesMatching(['fabric.mod.json', '*.mixins.json']) {
		expand(stringProperties) {
			escapeBackslash = true
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = project.java_version as Integer
}

java {
	targetCompatibility = sourceCompatibility = JavaVersion.toVersion(project.java_version as Integer)
	withSourcesJar()
}

jar {
	inputs.property 'archivesName', project.base.archivesName

	from('LICENSE') {
		rename { "${it}_${inputs.properties.archivesName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

tasks.register('deleteHeaders') {
	doLast {
		// delete "header files" from jar
		var jar = remapJar.archiveFile.get().asFile.toPath()
		if (Files.exists(jar)) {
			var jarFileUri = new URI('jar:file', jar.toUri().getSchemeSpecificPart(), jar.toUri().getFragment())

			try (FileSystem fs = FileSystems.newFileSystem(jarFileUri, ['create': 'false'])) {
				def p = fs.getPath("com/faboslav")
				if (Files.exists(p)) {
					Files.walkFileTree(fs.getPath("com/faboslav"), new DeletingFileVisitor());
				}
			}
		}
	}
}

tasks.build.finalizedBy('deleteHeaders')

class DeletingFileVisitor extends SimpleFileVisitor<Path> {
	@Override
	FileVisitResult visitFile(Path path, BasicFileAttributes basicFileAttributes) throws IOException {
		Files.delete(path);
		return FileVisitResult.CONTINUE;
	}

	@Override
	FileVisitResult postVisitDirectory(Path directory, IOException ioException) throws IOException {
		Files.delete(directory);
		return FileVisitResult.CONTINUE;
	}
}

def changesMd = file('changes.md').exists() ? file('changes.md').getText('UTF-8') : ''
def toList = (String s) -> s.split(',').collect{ it.trim() }
def gameVersionList = toList(project.game_version_list)

modrinth {
	token = project.provider {
		def token = System.getenv('MODRINTH_TOKEN')
		if (token == null) throw new GradleException("MODRINTH_TOKEN not defined")
		return token
	}
	projectId = project.modrinth_id
	versionName = project.mod_version
	versionNumber = project.version
	versionType = 'release'
	uploadFile = remapJar
	changelog = changesMd
	gameVersions = gameVersionList
	loaders = ['fabric']
	dependencies {
		toList(project.modrinth_dependencies).each { required.project(it) }
	}
}

tasks.modrinth.dependsOn("build") // ensure finalizer to delete "header files" runs

curseforge {
	def token = System.getenv("CURSEFORGE_TOKEN")
	if (token != null) {
		apiKey = token
	}

	project {
		id = project.curseforge_id
		releaseType = 'release'
		changelogType = 'markdown'
		changelog = changesMd

		addGameVersion 'Fabric'
		gameVersionList.each { addGameVersion(it) }

		relations {
			toList(project.curseforge_dependencies).each { requiredDependency(it) }
		}
		mainArtifact(remapJar.archiveFile.get().asFile) {
			displayName = "IndyPets ${project.mod_version} [${versionRange(gameVersionList)}]"
		}
		afterEvaluate {
			uploadTask.dependsOn('build')
		}
	}
	options {
		forgeGradleIntegration = false
		javaVersionAutoDetect = false
	}
}

minepkg {
	apiKey = project.provider {
		def token = System.getenv('MINEPKG_TOKEN')
		if (token == null) throw new GradleException("MINEPKG_TOKEN not defined")
		return token
	}
	artifact = remapJar
	platform = 'fabric'
	name = project.minepkg_id
	version = project.mod_version + "+" + project.minecraft_version
	license = "MIT"
	minecraftVersionRange = project.game_version_range
	toList(project.minepkg_dependencies).each { require(it) }
}

static String versionRange(List<String> gameVersions) {
	if (gameVersions.size() == 1)
		return gameVersions[0]

	def v0 = gameVersions.first()
	def v1 = gameVersions.last()

	def s0 = v0.split('[.]').collect { it as Integer}
	def s1 = v1.split('[.]').collect { it as Integer}

	// 1.19.3, 1.19.4 -> 1.19.3/4
	if (s0.size() == 3 && s1.size() == 3 && s0[0] == s1[0] && s0[1] == s1[1] && s1[2] - s0[2] == 1) {
		return "${s1[0]}.${s1[1]}.${s0[2]}/${s1[2]}"
	}

	return "${v0} - ${v1}"
}
